buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://repo.spring.io/libs-release" }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'checkstyle'

    group 'nl.fhict.happynews'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://maven.google.com" }
    }

    checkstyle {
        toolVersion = "7.6.1"
    }

    tasks.withType(Checkstyle).matching({ task -> !task.name.startsWith('checkstyleGenerated')}).all {
        reports {
            xml.enabled false
            html.enabled true
        }

        rootProject.tasks.checkstyle.dependsOn it
    }

    tasks.withType(JacocoReport) {
        rootProject.tasks.generateCoverageReports.dependsOn it
    }
}

// Don't generate .ipr and .iws files when running 'idea' task
ideaProject.enabled = false
ideaWorkspace.enabled = false

task clean {
    delete "${buildDir}/"
}

task installGitHooks(type: Copy) {
    from new File(rootProject.rootDir, 'hooks/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
}

task checkstyle

task generateCoverageReports
