apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "nl.fhict.happynews.android"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 3
        versionName "1.2"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Do not abort on lint errors for now
        lintOptions {
            abortOnError false
        }

        resValue 'string', 'application_id', applicationId
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        production {
        }
        local {
            applicationIdSuffix ".local"
            versionNameSuffix "-local"

            resValue 'string', 'application_id', defaultConfig.applicationId + applicationIdSuffix
        }
    }
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.koushikdutta.ion:ion:2.+'
    compile 'com.android.support:customtabs:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.google.android.gms:play-services-ads:11.2.0'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.3.1'
}

jacocoAndroidUnitTestReport {
    html.enabled true
    xml.enabled true
}

task checkstyleMain(type: Checkstyle) {
    configFile file("config/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("config/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

check.dependsOn(checkstyleMain)
